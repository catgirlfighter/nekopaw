[main]

$main.url='http://g.e-hentai.org/';
$main.icon='g.e-hentai.org.bmp';
$main.short = 'e-h';

$picture.exif.title = "%title";
$picture.exif.theme = "%album";
$picture.exif.comment = "%post_url";
$main.keywordhint = "source - album's URL: http://url_to_album [http://g.e-hentai.org/g/000000/abcde13ddc/]
source - keywords: keywords. After the list done, select albums you need and start getting list again";

$userstring = 'UserName';
$pwdstring = 'PassWord';

$main.checkcookie = 'ipb_member_id';
$main.login = "'https://forums.e-hentai.org/index.php?act=Login&CODE=01'";
$main.loginpost = "'returntype=8&CookieDate=1&b=d&bt=pone&' + $userstring + '=' + $login + '&' + $pwdstring + '=' + $password + '&ipb_login_submit=Login%21'";
$picture.fields(url,preview,post_url,alt_url,album_url,album,album_id,title,fsz,original_name,archive,archive_url,page:i,pcount:i);

@addfield("source","Source","indexlistbox",1,"album's URL","keywords");
@addfield("archives","Download Archives (spends Credits)","checkbox","False");
@addfield("fullsize","Download Fullsized","checkbox","False");
@addfield("autoreset","Reset FS Limit (spends GP)","checkbox","False");

@addfield("expunged","Expunged","checkbox","False");
@addfield("downvoted","Downvoted","checkbox","False");

@addfield("artistcg","Artist CG","checkbox","True");
@addfield("asianporn","Asian Porn","checkbox","True");
@addfield("cosplay","Cosplay","checkbox","True");
@addfield("doujinshi","Doujinshi","checkbox","True");
@addfield("gamecg","Game CG","checkbox","True");
@addfield("imageset","Image Set","checkbox","True");
@addfield("manga","Manga","checkbox","True");
@addfield("misc","Misc","checkbox","True");
@addfield("nonh","NON-H","checkbox","True");
@addfield("western","Western","checkbox","True");

$picsperpage = 20;
$picture.template.ext = '%ext%';

$main.picdelay = 5000;
$main.pagedelay = 3000;

$main.delaypostprocess = '1';
$main.loginscript = 'login';

[login]

$thread.loginresult = 1;

?@isempty($login)=0{
	$thread.loginresult = !@isempty(@cookie("ipb_member_id"));
}

?!$thread.loginresult{
	$thread.loginresult = 0;	
	$thread.url = $main.login;
	$thread.post = $main.loginpost;
	$thread.method = 'post';
	@thread.execute;

	$thread.loginresult = !@isempty(@cookie("ipb_member_id"));

	?$thread.loginresult{
		$thread.url = "'" + $main.url+"uconfig.php'";
		$thread.xml = "uconfig";
		$thread.method = "get";
		@thread.execute;

		
		$thread.method = "post";
		$thread.xml = "";
		$thread.url = "'" + $main.url + "uconfig.php?uh=y&rx=0&ry=0&dm=l&prn=y&f_doujinshi=on&f_manga=on&f_artistcg=on"
		+ "&f_gamecg=on&f_western=on&f_non-h=on&f_imageset=on&f_cosplay=on&f_asianporn=on&f_misc=on&favorite_0=Favorites+0"
		+ "&favorite_1=Favorites+1&favorite_2=Favorites+2&favorite_3=Favorites+3&favorite_4=Favorites+4&favorite_5=Favorites+5"
		+ "&favorite_6=Favorites+6&favorite_7=Favorites+7&favorite_8=Favorites+8&favorite_9=Favorites+9&rc=0&tl=m&ts=m"
		+ "&tr=" + $tr + "&hp=&hk=&tf=n&oi=" + $oi + "&qb=n&apply=Apply'";

		@thread.execute;
	}
}

[listscript]

@createcookie("nw","1");

?$archives="True"{
	$thread.extfromheader = 0;
}


?$source = 0{

	?$archives="True"{
		$thread.xmlcontent = 'html';
		$thread.method = 'get';
		$tag = @removevars($tag);
		$thread.url = "'" + $tag  + "'";
		$thread.xml = 'archivexml';
		$thread.postprocess = 'post';

		@thread.execute;	
	}
	
	?$archives!"True"{
		$thread.xmlcontent = 'html';
		$thread.method = 'get';
		$tag = @removevars($tag);
		$thread.url = "'" + $tag  + "?p=' + $thread.counter";
		$thread.xml = 'listxml';

		@thread.execute;
	}
}

?$source = 1{
	$thread.addunchecked = 1;
	$thread.xmlcontent = 'html';
	$thread.method = 'get';
	$thread.xml = 'searchxml';
	$thread.postprocess = 'post';

;http://g.e-hentai.org/?f_doujinshi=0&f_manga=0&f_artistcg=0&f_gamecg=0&f_western=0&f_non-h=1
;&f_imageset=0&f_cosplay=0&f_asianporn=0&f_misc=0&f_search=rachel+alucard&f_apply=Apply+Filter

	$thread.url = "'" + $main.url + "?page=' + $thread.counter + '"
	+ "&f_doujinshi=" + @boolint($doujinshi)
	+ "&f_manga=" + @boolint($manga) + "&f_artistcg=" + @boolint($artistcg)
	+ "&f_gamecg=" + @boolint($gamecg) + "&f_western=" + @boolint($western)
	+ "&f_non-h=" + @boolint($nonh) + "&f_imageset=" + @boolint($imageset)
	+ "&f_cosplay=" + @boolint($cosplay) + "&f_asianporn=" + @boolint($asianporn)
	+ "&f_misc=" + @boolint($misc) + "&f_search=" + @httpencode($tag) + "&f_apply=Apply+Filter";


	?($expunged="True"){$thread.url = $thread.url + "&f_sh=on";}
	?($downvoted="True"){$thread.url = $thread.url + "&f_sdt2=on";}

	$thread.url = $thread.url + "'";

	@thread.execute;
}

[listxml]
^table#class="ptt"{^a{
	?$thread.count < @ifempty(@urlvar("p",#href),0) + 1 { $thread.count = @ifempty(@urlvar("p",#href),0) + 1; }
}}

$album_name = "";
$oname = "";

^h1#id="gn"{
	$album_name = @text;
}

^div#id="gdt"{
	^a{
		@addpicture(
			%post_url = #href,
			%alt_url = #href,
			%album_url = $tag,
			%title = @trim(@text),
			%album = $album_name,
			%album_id = @copyfromtoex("/","/g/",$tag),
			%original_name = $oname
		);
		$picture.filename = @emptyname(#href) + ".%ext%";
	}
}

[dwscript]

?(@isempty($picture%archive)!0)&($picture%page!""){
	$thread.skip=1;
}

$needreset = 0;

?$picture%page=""{
	%url = "";
	$thread.url = "'" + $picture%alt_url + "'";
	$thread.xml = "pagexml";
	@thread.execute;
	?$needreset=1{
		$thread.url = "'http://g.e-hentai.org/home.php?act=limits&reset=Reset+Limit'";
		$thread.xml = 'limit';
		$thread.method = 'post';
		@thread.execute;
		$thread.method = 'get';
	}
	;$picture.makename;
	$thread.referer = $picture%post_url;
	$thread.url = $picture%url;
}

?@isempty($picture%archive)=0{
	@picture.makename;
	$thread.url = $picture%archive_url;
	$thread.referer = $picture%archive_url;
}

[limit]
$d = 0;
^div#class="d"{
	^p#id="continue"{
		$d=1;
	}
}

?$d=0{
	@thread.SendStop("Can not reset Full Size Limit",1);
}

[pagexml]

$tmp = "";

^div#id="i1"{
	^div#id="i3"{^img{
		$tmp = #src;
	}}

	?@emptyname($tmp)="509s.gif"{
		?$autoreset="True"{ $needreset=1; }
		?$autoreset!"True"{ @thread.SendStop("509 Banwidth exeeded",1); }
	}

	^div#id="i6"{^a{?@pos(#onclick,"return nl(")=1{
		%alt_url = $picture%alt_url + "&nl=" + @copyfromtoex(")","(",#onclick);
	}}}

	?$fullsize="True"{
		^div#id="i7"{^a{
			%url = @thread.trackredirect(#href);
			?@pos($picture%url,"fullimg.php")!0{
				?$autoreset="True"{ $needreset=1; }
				?$autoreset!"True"{ @thread.SendStop("Full Size limit exeeded",1); }
			}
		}}
	}

	?@isempty($picture%url)!0{
		%url = $tmp;
	}
	
	$tmp = @urlvar("n",$picture%url);
	?@isempty($tmp)!0{$tmp = @emptyname(@removevars($picture%url));}
	%original_name = N@changeext("",$tmp);
	$picture.filename = @changeext(@getext($tmp),$picture.filename);
	@picture.makename;
}

[uconfig]

$oi = "n";
$tr = "1";

^input#name="oi"{
	?(#value!"n")&(#value!"ohnoyoudont"){
		$oi = #value;
	}
}

^input#name="tr"{
	?(#disabled!"disabled"){
		$tr = #value;
	}
}

[searchxml]


^table#class="itg"{
	^div#class="it5"{
		^a{
			@addpicture(
				%album = @text,
				%album_id = @copyfromtoex("/","/g/",#href),
				%post_url = #href,
				%page = 0
			);
		}
	}
}

^table#class="ptb"{
	^a{
		?@ifempty(@urlvar("page",#href),0) + 1 > $thread.count{
			$thread.count = @urlvar("page",#href) + 1;
		}
	}
}

[archivexml]

^h1#id="gn"{
	@addpicture(
		%album = @text,
		%album_id = @copyfromtoex("/","/g/",$tag),
		%post_url = $tag,
		%page = 0
	);
}


[archivemain]

^div#class="gm"{^div#id="gright"{^div#id="gd5"{^p#class="g2"{
	^a{
		?@pos(#onclick,"/archiver.php?")>0{
			%archive_url = @copyfromtoex("',","popUp('",#onclick);
			$archivestage = 1;
		}
	}
}}}}

[archivepage]

^div#id="db"{
	$n = 0;
	^input#name="dlcheck"{
		?#disabled="disabled"{
			$error = #value;
		}
		?#disabled!"disabled"{
			$archivestage = 2;
		}
		$n = 1;
	}

	^p#id="continue"{
		^a{
			%archive_url = #href;
			$archivestage = 3;
			$n = 1;
		}
	}

	?$n=0{
		^strong{
			%archive = @text;
		}
		^a{
			%archive_url = @thread.trackredirect(@checkproto($picture%archive_url,#href));
			$archivestage = 4;		
		}
	}
}

[post]
$error = "";

?$picture.haveparent=0{
	$thread.addunchecked = 0;

	$thread.url = "'" + $picture%post_url + "?p=' + $picture%page";
	$thread.xmlcontent = "html";
	$thread.method = "get";
	?$archives="True"{
		$archivestage = 0;
		$thread.xml = "archivemain";
		@thread.execute;
		?$archivestage = 1{
			$thread.url = "'" + $picture%archive_url + "'";
			$thread.method = "get";
			$thread.xml = "archivepage";
			@thread.execute;

			?$archivestage = 2{
				$thread.method = "post";
				$thread.post = "'dlcheck=Download+Archive'";
				@thread.execute;	
			}

			?$archivestage = 3{
				$thread.url = "'" + $picture%archive_url + "'";
				$thread.method = "get";
				$thread.xml = "archivepage";
				@thread.execute;
			}
			
			?$archivestage = 4{
				$picture.filename = @emptyname(@removevars($picture%archive_url)) + ".zip";
			}

			?$archivestage!4{
				%archive="";
				@error("archive can't be downloaded - " + $error);
			}

		}
	}
	?$archives!"True"{
		$thread.xml = "searchlistxml";
		?@isempty($picture%pcount)!0{%pcount = 1;}
		$pcount = $picture%page; 
		:$pcount<$picture%pcount{
			?$thread.canceled=0{
				@thread.execute;
				?$thread.canceled=0{
					%page = $picture%page + 1;
					$pcount = $picture%page;
				}
			}
			?$thread.canceled!0{
				$pcount = $picture%pcount;
			}
		}
	}
}

[searchlistxml]

^table#class="ptt"{^a{
	?$picture%pcount < @ifempty(@urlvar("p",#href),0) + 1 { %pcount = @ifempty(@urlvar("p",#href),0) + 1; }
}}

$album_name = "";
$oname = "";

^h1#id="gn"{
	$album_name = @text;
}

^div#id="gdt"{
	^a{
		@addchild(
			%post_url = #href,
			%alt_url = #href,
			%album_url = $picture%post_url,
			%title = @trim(@text),
			%album = $album_name,
			%album_id = @copyfromtoex("/","/g/",$picture%post_url),
			%original_name = $oname
		);
		$child.filename = @emptyname(#href) + ".%ext%";
	}
}